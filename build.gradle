plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
        runs {
            client {
                vmArgs '-XX:+AllowEnhancedClassRedefinition', '-XX:HotswapAgent=fatjar'
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${parchment_version}@zip")
        }
    }

    processResources {
        var expandProps = [
            minecraft_version    : minecraft_version,
            mod_id               : mod_id,
            mod_name             : mod_name,
            mod_version          : mod_version,
            mod_license          : mod_license,
            mod_authors          : mod_authors,
            mod_description      : mod_description,
            mod_homepage         : mod_homepage,
            mod_github           : mod_github,
            required_exposure_version_range_forge : required_exposure_version_range_forge,
            required_exposure_version_range_fabric : required_exposure_version_range_fabric
        ]
        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', "*.mixins.json"]) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = "${mod_id}-${minecraft_version}"
    }

    version = rootProject.mod_version
    group = rootProject.mod_group_id

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            // location of the maven that hosts JEI files before January 2023
            name = "Progwml6's maven"
            url = "https://dvs1.progwml6.com/files/maven/"
        }
        maven {
            name = 'tterrag maven'
            url = 'https://maven.tterrag.com/'
        }
        maven {
            // location of the maven that hosts JEI files since January 2023
            name = "Jared's maven"
            url = "https://maven.blamejared.com/"
        }
        maven {
            // location of a maven mirror for JEI files, as a fallback
            name = "ModMaven"
            url = "https://modmaven.dev"
        }
        maven {
            url = "https://cursemaven.com"
        }
        maven {
            url "https://maven.shedaniel.me"
        } // REI
        maven {
            // Shedaniel's maven (Architectury API)
            url = "https://maven.architectury.dev"
            content {
                includeGroup "dev.architectury"
            }
        }
        maven {
            // saps.dev Maven (KubeJS and Rhino)
            url = "https://maven.saps.dev/releases"
            content {
                includeGroup "dev.latvian.mods"
            }
        }
        // EMI
        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/"
        }
        maven {
            name = "Mortuusars Github Maven"
            url = "https://raw.githubusercontent.com/mortuusars/resources/main/maven/"
        }
    }

    dependencies {
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

/**
 * Gets the first part (topmost) of a changelog file.
 * Expects it to be in the following format:
 * [
 *  ## latest version
 *      changes
 *
 *  ## older version
 *      changes
 * ]
 * Will return an empty string if failed.
 */
def getLatestVersionChangelog() {
    def changelogFileName = 'CHANGELOG.md'
    def changelogFile = file(changelogFileName)
    if (changelogFile.exists()) {
        def changelogContent = changelogFile.text
        def changelogParts = changelogContent.split(/(?m)^##\s/)

        if (changelogParts.size() > 1) {
            return "## " + changelogParts[1].trim() // First part will always be second in the list
        } else {
            println("[WARN] Changelog does not contain any versions.")
            return ""
        }
    } else {
        println("[WARN] Changelog file <${changelogFileName}> does not exist.")
        return ""
    }
}

project.ext.changelog = getLatestVersionChangelog()

task printLatestVersionChangelog {
    doLast {
        println project.changelog
    }
}